#!/usr/local/bin/expect --
#
# @(!--#) @(#) pushfile.exp, version 003, 08-november-2000
#

##############################################################################

proc shprompt {} {

  global progname
  
  expect {
    "\[\r\n]sh$ " {
      #
    }
    timeout {
      send_error "$progname: timeout waiting for custom sh prompt\n"
      exit 1
    }
    eof {
      send_error "$progname: eof waiting for custom sh prompt\n"
      exit 1
    }
  }

  return
}

##############################################################################

proc handlemetachars {line} {

  set mline ""

  for {set i 0} {$i < [string length $line]} {incr i} {
    set c [string index $line $i]

    if {[string compare $c "\'"] == 0} {
      set mline "$mline\\0047"
      continue
    }

    if {[string compare $c "\\"] == 0} {
      set mline "$mline\\0134"
      continue
    }

    set mline "$mline$c"
  }

  return $mline
}

##############################################################################

#
# Main
#

set progname $argv0

if {$argc < 1} {
  send_error "$progname: no filename specified\n"
  exit 1
}

set filename [lindex $argv 0]

if {[catch {set file [open "$filename" "r"]} errmsg]} {
  send_error "$progname: cannot open \"$filename\" for reading\n"
  exit 1
}

set timeout 60

spawn sh

expect {
  "$ " {
    #
  }
  timeout {
    send_error "$progname: timeout waiting for sh prompt\n"
    exit 1
  }
  eof {
    send_error "$progname: eof waiting for sh prompt\n"
    exit 1
  }
}

send "PS1=\"sh$ \"\r"
shprompt

send "PS2=\"\"\r"
shprompt

send "stty erase \"^H\"\r"
shprompt

while {1} {
  interact + break
}

puts ""
puts ""
puts "Now sending file"
puts ""

send "    PS1=\"sh$ \"\r"
shprompt

send "PS2=\"\"\r"
shprompt

send "cp /dev/null $filename\r"
shprompt

set linenum 0

while {[gets $file line] != -1} {
  incr linenum


  if {($linenum % 50) == 0} {
    send "# $linenum\r"
    shprompt
  }

  set line [handlemetachars $line]

  send "echo '$line' >>$filename\r"
  shprompt
}

close $file

puts ""
puts ""
puts "File Sent - Back to you"

send "\r"

interact

puts "The $progname expect script has finished"

exit 0
